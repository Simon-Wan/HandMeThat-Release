(define (domain igibson)
    (:requirements :strips :adl)
    (:types
        robot phyobj - object
    )
    (:predicates
        (robot-at ?r - robot ?o - phyobj)
        (holding ?r - robot ?o - phyobj)
        (human-holding ?o - phyobj)
        (hand-empty ?r - robot)

        (movable ?o - phyobj)
        (receptacle ?o - phyobj)

        (inside ?o - phyobj ?o - phyobj)
        (ontop ?o - phyobj ?o - phyobj)
        (has-inside ?o - phyobj)
        (has-ontop ?o - phyobj)

        (has-size ?o - phyobj)
        (has-color ?o - phyobj)
        (size-large ?o - phyobj)
        (size-small ?o - phyobj)
        (color-red ?o - phyobj)
        (color-blue ?o - phyobj)
        (color-green ?o - phyobj)

        (type-human ?o - phyobj)

		(type-floor ?o - phyobj)
		(type-countertop ?o - phyobj)
		(type-sofa ?o - phyobj)
		(type-bed ?o - phyobj)
		(type-stove ?o - phyobj)
		(type-table ?o - phyobj)
		(type-shelf ?o - phyobj)
		(type-toilet ?o - phyobj)
		(type-cabinet ?o - phyobj)
		(type-bathtub ?o - phyobj)
		(type-microwave ?o - phyobj)
		(type-oven ?o - phyobj)
		(type-dishwasher ?o - phyobj)
		(type-refrigerator ?o - phyobj)
		(type-sink ?o - phyobj)
		(type-pool ?o - phyobj)
		(type-highchair ?o - phyobj)
		(type-chair ?o - phyobj)
		(type-seat ?o - phyobj)
		(type-bottle ?o - phyobj)
		(type-jar ?o - phyobj)
		(type-kettle ?o - phyobj)
		(type-caldron ?o - phyobj)
		(type-bowl ?o - phyobj)
		(type-mug ?o - phyobj)
		(type-plate ?o - phyobj)
		(type-dish ?o - phyobj)
		(type-cup ?o - phyobj)
		(type-saucepan ?o - phyobj)
		(type-pan ?o - phyobj)
		(type-teapot ?o - phyobj)
		(type-blender ?o - phyobj)
		(type-casserole ?o - phyobj)
		(type-duffel_bag ?o - phyobj)
		(type-sack ?o - phyobj)
		(type-backpack ?o - phyobj)
		(type-briefcase ?o - phyobj)
		(type-bucket ?o - phyobj)
		(type-tray ?o - phyobj)
		(type-basket ?o - phyobj)
		(type-box ?o - phyobj)
		(type-package ?o - phyobj)
		(type-ashcan ?o - phyobj)
		(type-xmas_stocking ?o - phyobj)
		(type-xmas_tree ?o - phyobj)
		(type-apple ?o - phyobj)
		(type-banana ?o - phyobj)
		(type-melon ?o - phyobj)
		(type-grape ?o - phyobj)
		(type-lemon ?o - phyobj)
		(type-orange ?o - phyobj)
		(type-peach ?o - phyobj)
		(type-strawberry ?o - phyobj)
		(type-raspberry ?o - phyobj)
		(type-date ?o - phyobj)
		(type-olive ?o - phyobj)
		(type-chestnut ?o - phyobj)
		(type-carrot ?o - phyobj)
		(type-radish ?o - phyobj)
		(type-tomato ?o - phyobj)
		(type-broccoli ?o - phyobj)
		(type-mushroom ?o - phyobj)
		(type-onion ?o - phyobj)
		(type-lettuce ?o - phyobj)
		(type-pumpkin ?o - phyobj)
		(type-pop ?o - phyobj)
		(type-beer ?o - phyobj)
		(type-juice ?o - phyobj)
		(type-water ?o - phyobj)
		(type-milk ?o - phyobj)
		(type-beef ?o - phyobj)
		(type-chicken ?o - phyobj)
		(type-pork ?o - phyobj)
		(type-fish ?o - phyobj)
		(type-egg ?o - phyobj)
		(type-catsup ?o - phyobj)
		(type-sauce ?o - phyobj)
		(type-parsley ?o - phyobj)
		(type-tea_bag ?o - phyobj)
		(type-sugar ?o - phyobj)
		(type-vegetable_oil ?o - phyobj)
		(type-cracker ?o - phyobj)
		(type-bread ?o - phyobj)
		(type-cookie ?o - phyobj)
		(type-cake ?o - phyobj)
		(type-chip ?o - phyobj)
		(type-hamburger ?o - phyobj)
		(type-sandwich ?o - phyobj)
		(type-candy ?o - phyobj)
		(type-oatmeal ?o - phyobj)
		(type-sushi ?o - phyobj)
		(type-salad ?o - phyobj)
		(type-soup ?o - phyobj)
		(type-pasta ?o - phyobj)
		(type-carving_knife ?o - phyobj)
		(type-hammer ?o - phyobj)
		(type-screwdriver ?o - phyobj)
		(type-scraper ?o - phyobj)
		(type-saw ?o - phyobj)
		(type-printer ?o - phyobj)
		(type-scanner ?o - phyobj)
		(type-facsimile ?o - phyobj)
		(type-modem ?o - phyobj)
		(type-calculator ?o - phyobj)
		(type-headset ?o - phyobj)
		(type-earphone ?o - phyobj)
		(type-mouse ?o - phyobj)
		(type-alarm ?o - phyobj)
		(type-toothbrush ?o - phyobj)
		(type-perfume ?o - phyobj)
		(type-makeup ?o - phyobj)
		(type-highlighter ?o - phyobj)
		(type-marker ?o - phyobj)
		(type-pen ?o - phyobj)
		(type-pencil ?o - phyobj)
		(type-dishtowel ?o - phyobj)
		(type-hand_towel ?o - phyobj)
		(type-rag ?o - phyobj)
		(type-scrub_brush ?o - phyobj)
		(type-broom ?o - phyobj)
		(type-vacuum ?o - phyobj)
		(type-soap ?o - phyobj)
		(type-shampoo ?o - phyobj)
		(type-detergent ?o - phyobj)
		(type-toothpaste ?o - phyobj)
		(type-fork ?o - phyobj)
		(type-spoon ?o - phyobj)
		(type-knife ?o - phyobj)
		(type-lamp ?o - phyobj)
		(type-candle ?o - phyobj)
		(type-necklace ?o - phyobj)
		(type-bracelet ?o - phyobj)
		(type-jewelery ?o - phyobj)
		(type-bow ?o - phyobj)
		(type-wreath ?o - phyobj)
		(type-ribbon ?o - phyobj)
		(type-hardback ?o - phyobj)
		(type-notebook ?o - phyobj)
		(type-book ?o - phyobj)
		(type-newspaper ?o - phyobj)
		(type-painting ?o - phyobj)
		(type-pad ?o - phyobj)
		(type-document ?o - phyobj)
		(type-gym_shoe ?o - phyobj)
		(type-sandal ?o - phyobj)
		(type-shoe ?o - phyobj)
		(type-sock ?o - phyobj)
		(type-hat ?o - phyobj)
		(type-sunglass ?o - phyobj)
		(type-shirt ?o - phyobj)
		(type-sweater ?o - phyobj)
		(type-underwear ?o - phyobj)
		(type-apparel ?o - phyobj)
		(type-tile ?o - phyobj)
		(type-plywood ?o - phyobj)
		(type-cube ?o - phyobj)
		(type-ball ?o - phyobj)

        (open ?o - phyobj)
        (cooked ?o - phyobj)
        (dusty ?o - phyobj)
        (frozen ?o - phyobj)
        (stained ?o - phyobj)
        (sliced ?o - phyobj)
        (soaked ?o - phyobj)
        (toggled ?o - phyobj)

        (openable ?o - phyobj)
        (cookable ?o - phyobj)
        (dustyable ?o - phyobj)
        (freezable ?o - phyobj)
        (stainable ?o - phyobj)
        (sliceable ?o - phyobj)
        (soakable ?o - phyobj)
        (toggleable ?o - phyobj)

        (valid-clean-pair ?o - phyobj ?o - phyobj)
    )


    (:action robot-move
        :parameters (?r - robot ?from - phyobj ?to - phyobj)
        :precondition (and (robot-at ?r ?from) (not (movable ?from)) (not (movable ?to)))
        :effect (and (robot-at ?r ?to) (not (robot-at ?r ?from)))
    )

    (:action robot-pick-up-at-location
        :parameters (?r - robot ?obj - phyobj ?loc - phyobj)
        :precondition (and (robot-at ?r ?loc) (hand-empty ?r)
                        (or (and (inside ?obj ?loc) (or (not (openable ?loc)) (open ?loc))) (ontop ?obj ?loc))
                        (not (movable ?loc)) (movable ?obj) (receptacle ?loc))
        :effect (and (not (hand-empty ?r)) (not (inside ?obj ?loc)) (not (ontop ?obj ?loc)) (holding ?r ?obj))
    )

    (:action robot-pick-up-from-receptacle-at-location
        :parameters (?r - robot ?obj - phyobj ?rec - phyobj ?loc - phyobj)
        :precondition (and (robot-at ?r ?loc) (hand-empty ?r)
                        (or (and (inside ?obj ?rec) (or (not (openable ?rec)) (open ?rec))) (ontop ?obj ?rec))
                        (or (and (inside ?rec ?loc) (or (not (openable ?loc)) (open ?loc))) (ontop ?rec ?loc))
                        (not (movable ?loc)) (movable ?rec) (movable ?obj)
                        (receptacle ?loc) (receptacle ?rec) (not (receptacle ?obj)))
        :effect (and (not (hand-empty ?r)) (not (inside ?obj ?rec)) (not (ontop ?obj ?rec)) (holding ?r ?obj))
    )

    (:action robot-put-inside-location
        :parameters (?r - robot ?obj - phyobj ?loc - phyobj)
        :precondition (and (robot-at ?r ?loc) (holding ?r ?obj)
                        (not (movable ?loc)) (movable ?obj) (receptacle ?loc)
                        (has-inside ?loc) (or (not (openable ?loc)) (open ?loc)))
        :effect (and (not (holding ?r ?obj)) (inside ?obj ?loc) (hand-empty ?r))
    )

    (:action robot-put-ontop-location
        :parameters (?r - robot ?obj - phyobj ?loc - phyobj)
        :precondition (and (robot-at ?r ?loc) (holding ?r ?obj)
                        (not (movable ?loc)) (movable ?obj) (receptacle ?loc)
                        (has-ontop ?loc))
        :effect (and (not (holding ?r ?obj)) (ontop ?obj ?loc) (hand-empty ?r))
    )

    (:action robot-put-inside-receptacle-at-location
        :parameters (?r - robot ?obj - phyobj ?rec - phyobj ?loc - phyobj)
        :precondition (and (robot-at ?r ?loc) (holding ?r ?obj)
                        (or (and (inside ?rec ?loc) (or (not (openable ?loc)) (open ?loc))) (ontop ?rec ?loc))
                        (has-inside ?rec) (or (not (openable ?rec)) (open ?rec))
                        (not (movable ?loc)) (movable ?rec) (movable ?obj)
                        (receptacle ?loc) (receptacle ?rec) (not (receptacle ?obj)))
        :effect (and (not (holding ?r ?obj)) (inside ?obj ?rec) (hand-empty ?r))
    )

    (:action robot-put-ontop-receptacle-at-location
        :parameters (?r - robot ?obj - phyobj ?rec - phyobj ?loc - phyobj)
        :precondition (and (robot-at ?r ?loc) (holding ?r ?obj)
                        (or (and (inside ?rec ?loc) (or (not (openable ?loc)) (open ?loc))) (ontop ?rec ?loc))
                        (has-ontop ?rec)
                        (not (movable ?loc)) (movable ?rec) (movable ?obj)
                        (receptacle ?loc) (receptacle ?rec) (not (receptacle ?obj)))
        :effect (and (not (holding ?r ?obj)) (ontop ?obj ?rec) (hand-empty ?r))
    )

    (:action robot-bring-to-human
        :parameters (?r - robot ?obj - phyobj ?human - phyobj)
        :precondition (and (robot-at ?r ?human) (holding ?r ?obj)
                        (movable ?obj) (type-human ?human))
        :effect (and (not (holding ?r ?obj)) (human-holding ?obj) (hand-empty ?r))
    )

    (:action robot-take-from-human
        :parameters (?r - robot ?obj - phyobj ?human - phyobj)
        :precondition (and (robot-at ?r ?human) (hand-empty ?r) (human-holding ?obj)
                        (movable ?obj) (type-human ?human))
        :effect (and (not (hand-empty ?r)) (not (human-holding ?obj)) (holding ?r ?obj))
    )

    (:action robot-open-location
        :parameters (?r - robot ?loc - phyobj)
        :precondition (and (openable ?loc) (robot-at ?r ?loc) (not (open ?loc)))
        :effect (and (open ?loc))
    )

    (:action robot-close-location
        :parameters (?r - robot ?loc - phyobj)
        :precondition (and (openable ?loc) (robot-at ?r ?loc) (open ?loc))
        :effect (and (not (open ?loc)))
    )

    (:action robot-open-receptacle-at-location
        :parameters (?r - robot ?rec - phyobj ?loc - phyobj)
        :precondition (and (openable ?rec) (robot-at ?r ?loc) (not (open ?rec))
                        (or (and (inside ?rec ?loc) (or (not (openable ?loc)) (open ?loc))) (ontop ?rec ?loc)))
        :effect (and (open ?rec))
    )

    (:action robot-close-receptacle-at-location
        :parameters (?r - robot ?rec - phyobj ?loc - phyobj)
        :precondition (and (openable ?rec) (robot-at ?r ?loc) (open ?rec)
                        (or (and (inside ?rec ?loc) (or (not (openable ?loc)) (open ?loc))) (ontop ?rec ?loc)))
        :effect (and (not (open ?rec)))
    )

    (:action robot-toggle-on-location
        :parameters (?r - robot ?loc - phyobj)
        :precondition (and (toggleable ?loc) (robot-at ?r ?loc) (not (toggled ?loc)))
        :effect (and (toggled ?loc))
    )

    (:action robot-toggle-off-location
        :parameters (?r - robot ?loc - phyobj)
        :precondition (and (toggleable ?loc) (robot-at ?r ?loc) (toggled ?loc))
        :effect (and (not (toggled ?loc)))
    )

    (:action robot-toggle-on-movable-at-location
        :parameters (?r - robot ?obj - phyobj ?loc - phyobj)
        :precondition (and (toggleable ?obj) (robot-at ?r ?loc) (not (toggled ?obj))
                        (or (and (inside ?obj ?loc) (or (not (openable ?loc)) (open ?loc))) (ontop ?obj ?loc)))
        :effect (and (toggled ?obj))
    )

    (:action robot-toggle-off-movable-at-location
        :parameters (?r - robot ?obj - phyobj ?loc - phyobj)
        :precondition (and (toggleable ?obj) (robot-at ?r ?loc) (toggled ?obj)
                        (or (and (inside ?obj ?loc) (or (not (openable ?loc)) (open ?loc))) (ontop ?obj ?loc)))
        :effect (and (not (toggled ?obj)))
    )

    (:action robot-heat-obj
        :parameters (?r - robot ?obj - phyobj ?loc - phyobj)
        :precondition (and (hand-empty ?r) (robot-at ?r ?loc) (cookable ?obj)
                        (or (type-microwave ?loc) (type-stove ?loc) (type-oven ?loc)) (toggled ?loc)
                        (or (inside ?obj ?loc) (ontop ?obj ?loc))
                        (not (receptacle ?obj)) (not (movable ?loc)))
        :effect (and (cooked ?obj) (not (toggled ?loc)) (not (frozen ?obj)))
    )

    (:action robot-cool-obj
        :parameters (?r - robot ?obj - phyobj ?loc - phyobj)
        :precondition (and (hand-empty ?r) (robot-at ?r ?loc) (inside ?obj ?loc) (freezable ?obj)
                        (or (type-refrigerator ?loc)) (toggled ?loc) (not (receptacle ?obj)) (not (movable ?loc)))
        :effect (and (frozen ?obj) (not (cooked ?obj)))
    )

    (:action robot-slice-obj
        :parameters (?r - robot ?obj - phyobj ?tool - phyobj ?loc - phyobj)
        :precondition (and (holding ?r ?tool) (type-knife ?tool) (sliceable ?obj)
                        (robot-at ?r ?loc) (ontop ?obj ?loc) (not (receptacle ?obj)) (not (movable ?loc)))
        :effect (and (sliced ?obj))
    )

    (:action robot-soak-obj
        :parameters (?r - robot ?obj - phyobj ?loc - phyobj)
        :precondition (and (robot-at ?r ?loc) (inside ?obj ?loc) (soakable ?obj)
                        (type-sink ?loc) (toggled ?loc) (not (receptacle ?obj)) (not (movable ?loc)))
        :effect (and (soaked ?obj) (not (toggled ?loc)))
    )

    (:action robot-clean-obj-at-location
        :parameters (?r - robot ?obj - phyobj ?tool - phyobj ?loc - phyobj)
        :precondition (and (holding ?r ?tool) (or (dustyable ?obj) (stainable ?obj))
                        (robot-at ?r ?loc) (movable ?obj) (not (movable ?loc))
                        (or (and (inside ?obj ?loc) (or (not (openable ?loc)) (open ?loc))) (ontop ?obj ?loc))
                        (or (not (soakable ?tool)) (soaked ?tool)) (valid-clean-pair ?obj ?tool))
        :effect (and (not (dusty ?obj)) (not (stained ?obj)))
    )

    (:action robot-clean-location
        :parameters (?r - robot ?tool - phyobj ?loc - phyobj)
        :precondition (and (holding ?r ?tool) (or (dustyable ?loc) (stainable ?loc))
                        (robot-at ?r ?loc) (not (movable ?loc))
                        (or (not (soakable ?tool)) (soaked ?tool)) (valid-clean-pair ?loc ?tool))
        :effect (and (not (dusty ?loc)) (not (stained ?loc)))
    )

)